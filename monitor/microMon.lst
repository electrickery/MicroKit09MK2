                          NAM MICROMON09
                      * REV 1.10
                      *
                      *PROGRAMME MONITEUR POUR MC 6809
                      *
                      *ASSEMBLE SUR EUROMAK LE 20 JUILLET 1982


                      *****************************************
                      *PROGRAMME REALISE PAR CLAUDE VICIDOMINI*
                      *****************************************
                      *SAISI LE 21 JANVIER 2013 PAR F LE DUIGOU
                      *FIN DE VERIFICATION LE 26 JANVIER 2013
                      *Reformatted for explicit comments and assembly on April 28th, 2021 by F.J. Kraan
                      *with asm6809 (https://www.6809.org.uk/asm6809) to identical binary
                      *again for the Flex assembler format a09 (https://github.com/electrickery/A09)


                      *DEFINITION DES FONCTIONS:
                      ***P - PUNCH CHARGE UNE ZONE MEMOIRE EN CASSETTE
                      ***L - LOAD CHARGE LE CONTENU D'UNE CASSETTE EN MEMOIRE
                      ***M - MEMORY EXAMINE ET CHANGE LE CONTENU D'UNE MEMOIRE
                      ***AB- ABORT MET SOUS CONTROLE MONITEUR SANS INITIALISATION
                      ***R - REGISTER DISPLAY
                      *      ORDRE DE VISUALISATION des REGISTRES:CC,A,B,DP,X,Y,U,PC,SP
                      *      LE CONTENU DES REGISTRES PEUT ETRE CHANGE A LA DEMANDE
                      ***I - INCREMENT
                      *      INCREMENTE D'UN PAS LORS D'UNE FONCTION MEMOIRE
                      *      VISUALISE UN REGISTRE APRES L'AUTRE LORS D'UNE INTERRUPTION.
                      *      STOCKE EN MEMOIRE LA DONNEE RENTREE
                      ***G - GO DEMARRE UN PROGRAMME APRES AVOIR PRECISE SON ADRESSE
                      *      PERMET D'EFFECTUER UN CALCUL D'OFFSET POUR UN ADRESSAGE
                      *      INDEXE OU POUR UN BRANCHEMENT RELATIF
                      ***CN- CONTINUE PERMET DE CONTINUER LE DEROULEMENT D'UN PROGRAMME
                      *      APRES UN "ABORT".
                      ***DN- DECREMENTATION: PERMET DE DECREMENTER D'UN PAS LORS D'UNE
                      *      FONCTION MEMOIRE OU DE VISUALISER LES REGISTRES DANS L'AUTRE
                      *      SENS.
                      ***OF- OFFSET: CALCULE L'OFFSET LONG OU COURT LORS D'UNE
                      *      FONCTION MEMOIRE ET LE PLACE AUTOMATIQUEMENT DANS LA
                      *      MEMOIRE DE PROGRAMME, PUIS RETOURNE DANS LA FONCTION
                      *      MEMOIRE.
                      *X-OF- POST OCTET: PERMET DE CALCULER L'OFFSET EN DECIMAL QU'IL FAUT
                      *      DONNER AU POST OCTET; LE PROGRAMME LE CALCULE EN HEXA ET LE
                      *      PLACE EN MEMOIRE.
                      ***BP- BREAKPOINT: INSERE UN SWI2 A UNE ADRESSE SPECIFIEE, VISUALISE
                      *      LE CONTENU DES REGISTRES DU MPU, PUIS PLACE L'INSTRUCTION EN MEM
                      *      A LA PLACE DU SWI2
                      *
                      *
                      *
                      *
                      ********************************************************************
                      **LA PILE EST LOCALISEE A1 PARTIR DE L'ADRESSE $0780 "JUSQU'A $07CC"
                      **LA RAM PART DE L'ADRESSE $0000 JUSQU'A $07AF
                      **LE MONITEUR EST LOCALISE ENTRE $E000 ET $E7FF
                      **LE PIA EST LOCALISE ENTRE $A004 ET $A007
                      ******************************************************************** 
                      ********************************************************************
                      *  LE RESET EST SITUE A L'ADRESSE $E219.
                      *  LA ROUTINE DE NMI EST A L'ADRESSE $E27C.
                      *  LE SWI EST A L'ADRESSE $E2A7.
                      *  LE SWI2 EST A L'ADRESSE $E27C.
                      *  LES AUTRES INTERRUPS SONT DEFINIES DS "EMPLACEMENT DES REGISTRES"
                      ********************************************************************
                              ORG $E000
                      *
                      * SCRUTATION DU CLAVIER *
                      *
                      *              0   1   2   3
 E000 06050403        KEYTBL  FCB $06,$05,$04,$03
                      *
                      *              4   5   6   7
 E004 02011514                FCB $02,$01,$15,$14
                      * 
                      *              8   9   A   B
 E008 13121125                FCB $13,$12,$11,$25
                      *
                      *              C   D   E   F
 E00C 24232221                FCB $24,$23,$22,$21

                      * CODAGE DES SEGMENTS *
                      *
                      *              0   1   2   3
 E010 7E065B1F        DIGTBL  FCB $7E,$06,$5B,$1F
                      *
                      *              4   5   6   7
 E014 273D7D0E                FCB $27,$3D,$7D,$0E
                      *
                      *              8   9   A   B
 E018 7F3F6F75                FCB $7F,$3F,$6F,$75
                      *
                      *              C   D   E   F
 E01C 78577969                FCB $78,$57,$79,$69


                      **************  GETKEY ROUTINE  **************
                      *SCRUTE LES LIGNES ET LES COLONNES DU CLAVIER*
                      ************ ALLUME LES AFFICHEURS ***********

 E020 342C            GETKEY  PSHS Y,DP,B
 A0                           SETDP $A0
 E022 86A0                    LDA #$A0
 E024 1F8B                    TFR A,DP
 E026 8D53            FINCLA  BSR DISPRE      ; ALLUMER LES AFFICHEURS
 E028 4F                      CLRA
 E029 9706                    STA <SCNREG     ; ACCES A DDRA
 E02B 9707                    STA <SCNCNT     ; ACCES A DDRB
 E02D 9704                    STA <DISREG     ; PA EN ENTREE
 E02F 860F                    LDA #$0F
 E031 9705                    STA <DISCNT     ; PB EN SORTIE
 E033 8604                    LDA #$04
 E035 9706                    STA <SCNREG     ; ACCES A ORA-DISREG
 E037 9707                    STA <SCNCNT     ; ACCES A DRB-DISCNT
 E039 C6FF                    LDB #$FF
 E03B 5C              LIGSUI  INCB
 E03C C104                    CMPB #$04       ; FIN SCRUT. CLAVIER?
 E03E 27E6                    BEQ FINCLA      ; OUI, SCRUTER AFFICHEURS
 E040 D705                    STB <DISCNT     ; NON, SCRUTER LIGNE PAR LIGNE
 E042 9604                    LDA <DISREG
 E044 43                      COMA            ; PAS DE TOUCHE ENFONCEES?
 E045 27F4                    BEQ LIGSUI      ; OUI, SCRUTER LIGNE SUIV;
 E047 F707F0                  STB SAVCNT     
 E04A B707F1                  STA SAVREG      ; NON, TOUCHE ENFONCEE
 E04D 4F                      CLRA
 E04E C601                    LDB #$01
 E050 F107F1          COLSUI  CMPB SAVREG     ; TOUCHE DETECTEE?
 E053 2706                    BEQ DECKEY      ; OUI, RECONNAITRE LA TOUCHE
 E055 4C                      INCA            ; NON, PASSER A LA COLONNE SUIVANTE
 E056 58                      ASLB            ; TOUTES COLONNES TESTEES?
 E057 27E2                    BEQ LIGSUI      ; OUI, LIGNE SUIVANTE
 E059 20F5                    BRA COLSUI      ; NON,COLONNE SUIVANTE


                      ************ RECONNAISSANCE DE LA TOUCHE ***********
                      *FABRIQUE LE CODE DE RECONNAISSANCE DE LA TOUCHE SI*
                      ** ELLE EST APPUYEE     ; SUPPRIME LES REBONDISSEMENTS**

 E05B F607F0          DECKEY  LDB SAVCNT      ; NUMERO DE LIGNE
 E05E 58                      ASLB
 E05F 58                      ASLB
 E060 58                      ASLB
 E061 58                      ASLB
 E062 3404                    PSHS B          ; REPERE LIGNE
 E064 ABE0                    ADDA ,S+
 E066 3402                    PSHS A          ; SAUVEGARDE CODE TOUCHE
 E068 108E0008                LDY #$0008
 E06C 5F              NOREB   CLRB
 E06D 9604            DLY2    LDA <DISREG
 E06F 43                      COMA            ; REBONDISSEMENT ?
 E070 26FA                    BNE NOREB       ; OUI, ATTENDRE DISPARITION
 E072 5A                      DECB            ; NON, TEMPO=30MS
 E073 26F8                    BNE DLY2
 E075 313F                    LEAY -1,Y
 E077 26F4                    BNE DLY2
 E079 35AE                    PULS PC,Y,DP,B,A    ; RETOUR RESET ROUTINE ; RTS


                      ****** ALLUMAGE DES AFFICHEURS ******


 E07B 3416            DISPRE  PSHS X,B,A
 E07D 8EA004                  LDX #DISREG
 E080 4F                      CLRA
 E081 A702                    STA 2,X             ; ACCES A DDRA
 E083 A703                    STA 3,X             ; ACCES A DDRB
 E085 86FF                    LDA #$FF            ; Set all port lines to output, decimal point too
 E087 A784                    STA ,X              ; PA EN SORTIE
 E089 860F                    LDA #$0F
 E08B A701                    STA 1,X             ; PB0-3 EN SORTIE
 E08D 8604                    LDA #$04
 E08F A702                    STA 2,X             ; ACCES A PA-DISREG
 E091 A703                    STA 3,X             ; ACCES A PB-DISCNT
 E093 8E07FA                  LDX #DISBUF
 E096 C603                    LDB #$03
 E098 5C              RECOM   INCB
 E099 C10A                    CMPB #$0A           ; TOUS LES AFFICHEURS SCRUTES?
 E09B 2602                    BNE SCRUTA          ; NON, CONTINUER
 E09D 3596                    PULS PC,X,B,A       ; OUI, RETOUR SOUS GETKEY ; RTS


                      ****** ALLUMER UN AFFICHEUR APRES L'AUTRE *******


 E09F F7A005          SCRUTA  STB >DISCNT         ; CHOISIR L'AFFICHEUR
 E0A2 A680                    LDA ,X+             ; PRENDRE CARACTERE DS DISBUF
 E0A4 BA0760                  ORA MABS            ; Set the dp-segment for absent memory
 E0A7 43                      COMA
 E0A8 B7A004                  STA DISREG          ; ALLUMER SEGMENTS
 E0AB 86A0                    LDA #$A0
 E0AD 4A              DLY1    DECA
 E0AE 26FD                    BNE DLY1            ; DUREE #1MS
                          
                          
 E0B0 86FF                    LDA #$FF            ; Clear all segments
 E0B2 B7A004                  STA DISREG          ;  before next display
                          
                          
 E0B5 20E1                    BRA RECOM           ; ALLUMER TOUS LES AFFICHEURS

                      *** CHARGEMENT DE L'ADRESSE DANS X ***


 E0B7 1E89            FADDRX  EXG A,B
 E0B9 1F01                    TFR D,X             ; D=X=ADRESSE PROGRAMME
 E0BB 3586                    PULS PC,B,A         ; RTS


                      ****** FABRICATION DES ADRESSES ******


 E0BD 3406            BADDR   PSHS B,A
 E0BF 4F                      CLRA
 E0C0 5F                      CLRB
 E0C1 FD07FA                  STD DISBUF
 E0C4 FD07FC                  STD DISBUF+2        ; 4 PREMIERS DIGITS=0
 E0C7 8E07FA                  LDX #DISBUF         ; POINTER SUR DISBUF
 E0CA 8D08                    BSR HEXIN7          ; AFFICHE 2 PREMIERS CHIFFRES
 E0CC 3402                    PSHS A
 E0CE 8D04                    BSR HEXIN7          ; AFFICHE 3e ET 4e CHIFFRE
 E0D0 3504                    PULS B
 E0D2 20E3                    BRA FADDRX          ; FABRIQUE ADRESSE
 E0D4 8D16            HEXIN7  BSR KEYHEX          ; FABRIQUE VAL TOUCHE
 E0D6 48                      ASLA
 E0D7 48                      ASLA
 E0D8 48                      ASLA
 E0D9 48                      ASLA                ; TRANSFERT LSB,MSB
 E0DA 3402                    PSHS A              ; SAUVE VAL TOUCHE
 E0DC 8D26                    BSR L7SEG           ; FABRIQUE VAL CONVERSION TOUCHE
 E0DE A780                    STA ,X+             ; VAL CONV DANS DISBUF
 E0E0 8D0A                    BSR KEYHEX          ; CONTINUER CHIFFRES SUIVANTS
 E0E2 ABE0                    ADDA ,S+
 E0E4 3402                    PSHS A
 E0E6 8D20                    BSR R7SEG
 E0E8 A780                    STA ,X+             ; TOUCHE SUIVANTE
 E0EA 3582                    PULS PC,A           ; RTS


                      **FABRICATION DE LA VALEUR HEXA DE LA TOUCHE**


 E0EC 17FF31          KEYHEX  LBSR GETKEY         ; SCRUTER LIGNES ET COLONNES
 E0EF 3414            HEXCON  PSHS X,B            ; ALLUMER CHIFFRE ET G
 E0F1 8EE000                  LDX #KEYTBL         ; POINTER SUR LE TABLEAU
 E0F4 C6FF                    LDB #$FF            ; DES CHIFFRES
 E0F6 5C              SCRUTC  INCB
 E0F7 8CE010                  CMPX #DIGTBL        ; VALEUR DIFF CHIFFRE?
 E0FA 272A                    BEQ FONCTI          ; OUI, C'EST UNE FONCTION
 E0FC A180                    CMPA ,X+            ; NON, CHIFFRE TROUVE?
 E0FE 26F6                    BNE SCRUTC          ; NON, CONTINUER A SCRUTER
 E100 1F98                    TFR B,A
 E102 3594                    PULS PC,X,B         ; RTS


                      *** CONVERSION HEXA-7 SEGMENTS ***
                      ********** TOUCHES VALEURS *******


 E104 47              L7SEG   ASRA
 E105 47                      ASRA
 E106 47                      ASRA
 E107 47                      ASRA            ; VAL TOUCHE DS A, LSB
 E108 3410            R7SEG   PSHS X
 E10A 8EE010                  LDX #DIGTBL     ; POINTER SUR TABLEAU
 E10D 840F                    ANDA #$0F       ; PREMIERE TOUCHE?
 E10F 2705            NDVALH  BEQ VALHEX      ; OUI, VAL HEX DS A
 E111 3001                    LEAX 1,X        ; NON, POINTER SUR VAL SUIVANTE
 E113 4A                      DECA
 E114 20F9                    BRA NDVALH      ; RECOMMENCER SI VAL NON TROUVEE
 E116 A684            VALHEX  LDA ,X          ; CONVERSION HEXA-7SEGMENTS
 E118 3590                    PULS PC,X       ; DANS A TROUVE ; RTS


                      **** CONVERSION DU CODE TOUCHE EN UNE VAL HEXA ****


 E11A 3414            CONHEX  PSHS X,B
 E11C 8EE010                  LDX #DIGTBL
 E11F 1F89                    TFR A,B         ; B=DISBUF+4 OU DISBUF+5
 E121 4F                      CLRA
 E122 E180            NONFIN  CMPB ,X+        ; CHERCHE VAL DONNEE
 E124 2707                    BEQ DONEA       ; A=VAL HEXA DONNEE
 E126 10270112        FONCTI  LBEQ RPOINT     ; OUI, FONCTION, RETOURNER SCRUTER
 E12A 4C                      INCA
 E12B 20F5                    BRA NONFIN
 E12D 3594            DONEA   PULS PC,X,B     ; RTS


                      ****** EXECUTION DE LA FONCTION MEMOIRE ******


 E12F 4F              EXMEMO  CLRA
 07                           SETDP $07
 E130 97FE                    STA <DISBUF+4   ; ETEINDRE 5e DIGIT
 E132 866E                    LDA #$6E
 E134 97FF                    STA <DISBUF+5   ; M DANS 6e DIGIT
 E136 8D85                    BSR BADDR       ; FABRIQUER ADRESSE DANS X
 E138 0FEE            REMEMO  CLR <COMDEC
 E13A A684                    LDA ,X          ; METTRE A DANS LE CONTENU
 E13C 3402                    PSHS A          ; DE LA CASE MEMOIRE
 E13E 8DC4                    BSR L7SEG       ; FABRIQUER LE CODE A METTRE
 E140 97FE                    STA <DISBUF+4   ; DANS LE 5e DIGIT
 E142 3502                    PULS A
 E144 8DC2                    BSR R7SEG       ; FABRIQUER LE CODE A METTRE
 E146 97FF                    STA <DISBUF+5   ; DANS LE 6e DIGIT
 E148 17FED5          ENCDON  LBSR GETKEY     ; ALLUMER LES DIGITS
 E14B 8136                    CMPA #$36       ; TOUCHE X?
 E14D 2766                    BEQ EXPOCT      ; OUI, SCRUTER LA TOUCHE OFFSET
 E14F 8100                    CMPA #$00       ; INCREMENTE CASE MEMOIRE?
 E151 272F                    BEQ EXINC       ; OUI, EXECUTER LA FONCTION
 E153 8110                    CMPA #$10       ; NON, DECREMENTE CASE MEMOIRE?
 E155 2729                    BEQ EXDEC       ; OUI, EXECUTE LA FONCTION
 E157 8133                    CMPA #$33       ; TOUCHE OFFSET?
 E159 102705C0                LBEQ EXOFST     ; OUI, EXECUTE LA FONCTION
 E15D 8D90                    BSR HEXCON      ; NON, FABRIQUER CODE HEXA TOUCHE
 E15F D6FF                    LDB <DISBUF+5   ; SHIFTER DISBUF
 E161 D7FE                    STB <DISBUF+4
 E163 8DA3                    BSR R7SEG
 E165 97FF                    STA <DISBUF+5
 E167 8D02                    BSR INCREM      ; STOCKER DONNEE
 E169 20DD                    BRA ENCDON      ; RECOM SI AUTRE DONNEE


                      ******* EXECUTION DE LA FONCTION INCREMENTATION *******
                      *********** ET DE LA FONCTION DECREMENTATION **********


 07                           SETDP $07
 E16B 96FE            INCREM  LDA <DISBUF+4 PLACE DONNEE EN MEM.
 E16D 8DAB                    BSR CONHEX      ; CONVERTIR VAL CONVERSION
 E16F 48                      ASLA
 E170 48                      ASLA
 E171 48                      ASLA
 E172 48                      ASLA            ; VAL HEXA DANS MSB
 E173 3402                    PSHS A
 E175 96FF                    LDA <DISBUF+5
 E177 8DA1                    BSR CONHEX
 E179 ABE0                    ADDA ,S+        ; A=DONNEE DISBUF+4 ET +5
 E17B 1F89                    TFR A,B
 E17D A784                    STA ,X          ; DONNEE DANS CASE X
 E17F 39                      RTS
                              
 E180 0AEE            EXDEC   DEC <COMDEC
 E182 8DE7            EXINC   BSR INCREM
 E184 96EE                    LDA <COMDEC     ; INCREMENTE OU DECREMENTE?
 E186 2627                    BNE DECRE       ; COMDEC<>0, DECREMENTE
 E188 A680                    LDA ,X+         ; PRENDRE DONNEE DS X ET X+1
 E18A 3404            PREXIN  PSHS B          ; SAUVE AVANT STOCKAGE
 E18C A1E0                    CMPA ,S+        ; MEMOIRE ABSENTE OU MEMOIRE MORTE?
                      ;        LBNE RPOINT     ; OUI, ALLUMER PROMPT ET SCRUTER
 E18E 17008F                  LBSR SETMBS     ; Set or reset 
 E191 1F10                    TFR X,D         ; NON, AFFICHE CASE MEMOIRE
 E193 17FF6E                  LBSR L7SEG      ; SUIVANTE OU PRECEDENTE
 E196 97FA                    STA <DISBUF
 E198 1F10                    TFR X,D
 E19A 17FF6B                  LBSR R7SEG
 E19D 97FB                    STA <DISBUF+1
 E19F 1F98                    TFR B,A
 E1A1 17FF60                  LBSR L7SEG
 E1A4 97FC                    STA <DISBUF+2
 E1A6 1F98                    TFR B,A
 E1A8 17FF5D                  LBSR R7SEG
 E1AB 97FD                    STA <DISBUF+3
 E1AD 2089                    BRA REMEMO      ; RECOMMENCER EXECUTION MEMOIRE
 E1AF A684            DECRE   LDA ,X
 E1B1 301F                    LEAX -1,X
 E1B3 20D5                    BRA PREXIN      ; DECREMENTATION EXECUTEE


                      ****** EXECUTION POST OCTET: DEFINI S'IL EST >0 OU <0 ******


 E1B5 8DB4            EXPOCT  BSR INCREM      ; POST OCTET EN MEM
 E1B7 97E4                    STA <SAVPOC     ; SAUVE POST OCTET
 E1B9 8601                    LDA #$01        ; ALLUME PROMPT
 E1BB 3410                    PSHS X
 E1BD 1700E9                  LBSR PROMPT
 E1C0 17FE5D                  LBSR GETKEY
 E1C3 8133                    CMPA #$33       ; TOUCHE OFFSET?
 E1C5 2703                    BEQ OFFSET
 E1C7 16FF6E                  LBRA REMEMO     ; NON, RETOUR FCT MEM
 E1CA 96E4            OFFSET  LDA <SAVPOC     ; REPRENDRE POST OCTET
 E1CC 3510                    PULS X
 E1CE 301F                    LEAX -1,X
 E1D0 1F89                    TFR A,B
 E1D2 C48E                    ANDB #$8E 
 E1D4 C18C                    CMPB #$8C       ; LEA N,PCR?
 E1D6 2610                    BNE CALPOT      ; NON, AFFICHE SIGNE
 E1D8 1F89                    TFR A,B 
 E1DA C40D                    ANDB #$0D
 E1DC C10C                    CMPB #$0C       ; BRANCH COURT
 E1DE 1027054E                LBEQ OFPOCT
 E1E2 C10D                    CMPB #$0D       ; BRANCH LONG
 E1E4 10270570                LBEQ EXLBCL 
 E1E8 17FE35          CALPOT  LBSR GETKEY
 E1EB 1700AF                  LBSR CLRDIS
 E1EE 8100                    CMPA #$00       ; TOUCHE INCREM?
 E1F0 2709                    BEQ PLUS        ; POSTOCTET>0
 E1F2 8110                    CMPA #$10       ; TOUCHE DECREM?
 E1F4 270C                    BEQ MOINS       ; POSTOCTET<0
 E1F6 3001                    LEAX 1,X
 E1F8 16FF3D                  LBRA REMEMO     ; SINON RETOUR MEM
 E1FB 866B            PLUS    LDA #$6B
 E1FD 97FF                    STA <DISBUF+5   ; AFFICHE PLUS
 E1FF 4F                      CLRA
 E200 2006                    BRA CONTI
                              
 E202 866E            MOINS   LDA #$6E
 E204 97FF                    STA <DISBUF+5   ; AFFICHE MOINS
 E206 8680                    LDA #$80
 E208 97E3            CONTI   STA <PLUSMS
 E20A 1601E8                  LBRA AFIVAL


                      ****** PLACE UN POINT D'ARRET EN MEMOIRE ET SAUVE L'INSTRUCTION ******


 E20D CC756B          BPOINT  LDD #$756B
 E210 DDFE                    STD <DISBUF+4   ; ALLUMER BP
 E212 17FEA8                  LBSR BADDR      ; AFFICHE ADRESSE
 E215 EC84                    LDD ,X
 E217 DDDF                    STD <SASWI2     ; SAUVE INSTRUCTION
 E219 CC103F                  LDD #$103F
 E21C ED84                    STD ,X          ; PLACE POINT D'ARRET
 E21E 201C                    BRA RPOINT      ; RETOUR SCRUT

                      SETMBS
 E220 3403                    PSHS A, CC
 E222 2603                    BNE SM1
 E224 4F                      CLRA
 E225 2002                    BRA SM2
 E227 8680            SM1     LDA #$80
 E229 9760            SM2     STA <MABS
 E22B 3503                    PULS A,CC
 E22D 39                      RTS

                      ***********************************************************************
                      ******************** PROGRAMME DE RESET *******************************
                      ****** INITIALISATION ET DECODAGE DES FONCTIONS :M, R,CN,G,BP,L,P *****
                      ***********************************************************************


 E22E 10CE07C0        RESTAR  LDS #PILE       ; INIT PILE
 E232 10FF07F8                STS >SAVPIL     ; ET POINTEUR X
 E236 8EE287                  LDX #ROUNMI
 E239 BF07DD                  STX >SAVNMI
 E23C 10CE07A0        RPOINT  LDS #PILMON
 07                           SETDP $07
 E240 8607                    LDA #$07        ; INIT DP
 E242 1F8B                    TFR A,DP
 E244 8D57                    BSR CLRDIS      ; DISBUF=0
 E246 8601                    LDA #$01
 E248 97FA                    STA <DISBUF     ; CHARGEMENT PROMPT
 E24A 17FDD3                  LBSR GETKEY     ; ALLUME PROMPT
 E24D 8126                    CMPA #$26       ; TOUCHE BP ?
 E24F 27BC                    BEQ BPOINT      ; OUI, PLACER POINT D'ARRET
 E251 8130                    CMPA #$30       ; TOUCHE REGISTRE?
 E253 102700C8                LBEQ FONREG     ; OUI, EXECUTE FONCTION DE CHGT REG
 E257 8120                    CMPA #$20       ; TOUCHE=MEMORY?
 E259 1027FED2                LBEQ EXMEMO     ; OUI, EXECUTE ROUTINE
 E25D 8132                    CMPA #$32       ; NON, TOUCHE=CONTINUE?
 E25F 2722                    BEQ EXCN        ; OUI, EXECUTE ROUTINE
 E261 8134                    CMPA #$34       ; NOU, TOUCHE=LOAD?
 E263 1027038A                LBEQ EXLOAD     ; OUI, EXECUTE ROUTINE
 E267 8135                    CMPA #$35       ; NON, TOUCHE=PUNCH?
 E269 102703EC                LBEQ EXPUNC     ; OUI, EXECUTE ROUTINE
 E26D 8131                    CMPA #$31       ; NON, TOUCHE=GO?
 E26F 26CB                    BNE RPOINT      ; NON, RETOUR SCRUTATION
 E271 867C                    LDA #$7C        ; OUI, CHARGE G DS DERNIER DIGIT
 E273 97FF                    STA <DISBUF+5
 E275 17FE45                  LBSR BADDR      ; FABRIQUE ADRESSE DEPART
 E278 109EF8                  LDY <SAVPIL     ; DU PROGRAMME
 E27B AF2A                    STX 10,Y        ; ADRESSE PROG DANS PC
 E27D 8680                    LDA #$80
 E27F AAA4                    ORA ,Y          ; POSITIONNER PLAG E=1
 E281 A7A4                    STA ,Y          ; POUR PRENDRE EN COMPTE
 E283 10DEF8          EXCN    LDS <SAVPIL     ; TOUS LES REGISTRES
 E286 3B                      RTI             ; DEPART PROG UTILISATEUR


                      ****** INITIALISATION DE LA FONCTION NMI ******


 E287 A66A            ROUNMI  LDA 10,S
 E289 84F0                    ANDA #$F0
 E28B 81E0                    CMPA #$E0
 E28D 27AD                    BEQ RPOINT
 E28F 202E                    BRA RSWI


                      ****** INITIALISATION DE LA FONCTION SWI2 ******


 E291 6A6B            RSWI2   DEC 11,S
 E293 6A6B                    DEC 11,S        ; POINTER PCR SUR INSTRUC.
 E295 FC07DF                  LDD >SASWI2
 E298 EDF80A                  STD [10,S]      ; REMETRE INSTRUCTION EN MEM.
 E29B 2022                    BRA RSWI        ; SCRUTER LES REGISTRES


                      ************** REMISE A ZERO DE TOUT DISBUF **************


 E29D 3456            CLRDIS  PSHS U,X,B,A
 E29F 338D0011                LEAU INTER2,PCR
 E2A3 3440                    PSHS U
 E2A5 4F                      CLRA            ; TOUT DISBUF=0
 E2A6 B70760                  STA MABS        ; clear decimal point flag
 E2A9 C606            PROMPT  LDB #$06
 E2AB 8E07FA                  LDX #DISBUF
 E2AE A780            ENCORE  STA ,X+
 E2B0 5A                      DECB
 E2B1 26FB                    BNE ENCORE
 E2B3 39                      RTS
                              
 E2B4 35D6            INTER2  PULS PC,U,X,B,A


                      * CODAGE DES REGISTRES DU MPU *
                      *
                      *              C   A   B   D
 E2B6 786F7557        REGTBL  FCB $78,$6F,$75,$57
                      *
                      *              X   Y   U   P
 E2BA 6737766B                FCB $67,$37,$76,$6B
                      *
                      *             S
 E2BE 3D                      FCB $3D


                      *************** SOFTWARE INTERRUPT ROUTINE *****************************
                      ***** PERMET DE VISUALISER LE CONTENU DES REGISTRES DU MPU *************
                      ******* ET DE CHANGER EVENTUELLEMENT LE CONTENU D'UN REGISTRE QCQ ******
                      ************************************************************************


 07                           SETDP $07
 E2BF 8607            RSWI    LDA #$07
 E2C1 1F8B                    TFR A,DP
 E2C3 10DFF8                  STS <SAVPIL
 E2C6 10CE07A0                LDS #PILMON
 E2CA 8DD1                    BSR CLRDIS      ; TOUT DISBUF=00
 E2CC C602                    LDB #$02        ; COMPTEUR REGISTRES
 E2CE 9EF8                    LDX <SAVPIL     ; INDEX DE CHARGEMENT DES REGISTRES
 E2D0 318CE3                  LEAY REGTBL,PCR ; Y=REGTBL
 E2D3 338D0035        TOUREG  LEAU >INTER,PCR
 E2D7 3440                    PSHS U          ; PC SAUVE EN PILE
 E2D9 A6A0                    LDA ,Y+         ; A=VAL REGISTRE
 E2DB 97FF                    STA <DISBUF+5   ; REGISTRE DS DERNIER DIGIT
 E2DD 108CE2BF                CMPY #RSWI      ; FIN TABLEAU?
 E2E1 2603                    BNE SUITER      ; NON
 E2E3 8E07F8                  LDX #SAVPIL     ; OUI, X POINTE SAVPIL
 E2E6 108CE2BA        SUITER  CMPY #REGTBL+4  ; 1iere MOITIER TEBLEAU?
 E2EA 230E                    BLS AF2DIG      ; OUI, 1iere MOITIER
 E2EC A684                    LDA ,X          ; NON, 2ieme MOITIER
 E2EE 17FE13                  LBSR L7SEG      ; AFFICHE CONTENU
 E2F1 97FA                    STA <DISBUF     ; SUR 4 DIGITS
 E2F3 A680                    LDA ,X+ 
 E2F5 17FE10                  LBSR R7SEG
 E2F8 97FB                    STA <DISBUF+1
 E2FA A684            AF2DIG  LDA ,X          ; 1iere MOITIER
 E2FC 17FE05                  LBSR L7SEG      ; AFFICHE COTENU SUR
 E2FF 97FC                    STA <DISBUF+2   ; 2 DIGITS
 E301 A680                    LDA ,X+ 
 E303 17FE02                  LBSR R7SEG
 E306 97FD                    STA <DISBUF+3
 E308 17FD15                  LBSR GETKEY     ; ALLUMER AFFICHEURS
 E30B 39                      RTS             ; RETOUR A CHANGEMENT DE REG
                              
 E30C 5A              INTER   DECB 
 E30D 2F1F                    BLE SCRUDE      ; DECREMENTE OU INCREMENTE ? 
 E30F C101                    CMPB #$01       ; 1er REGISTRE =CCR?
 E311 2715                    BEQ TSTDEC      ; OUI, SCUTER r
 E313 8100            REGSUI  CMPA #$00       ; REGISTRE SUIVANT?
 E315 1026FF23                LBNE RPOINT     ; NON, RETOUR PROMPT
 E319 108CE2BF                CMPY #RSWI      ; OUI, FIN TABLEAU?
 E31D 26B4                    BNE TOUREG      ; NON, CONTINUER REGISTRES
 E31F 17FF7B          FONREG  LBSR CLRDIS
 E322 8641                    LDA #$41        ; AFFICHER r
 E324 97FE                    STA <DISBUF+4
 E326 203F                    BRA EXREGI      ; SCRUTER REGISTRES
                              
 E328 8110            TSTDEC  CMPA #$10
 E32A 27F3                    BEQ FONREG
 E32C 20E5                    BRA REGSUI 
                              
 E32E 8110            SCRUDE  CMPA #$10       ; DECREMENTATION DEMANDEE?
 E330 2702                    BEQ REGPRE      ; OUI, PASSER AU REGISTRE PRECEDENT
 E332 20DF                    BRA REGSUI      ; NON, PASSER AU REGISTRE SUIVANT
                              
 E334 313E            REGPRE  LEAY -2,Y       ; REGTBL POINTE VERS REGISTRE PRECEDENT
 E336 17FF64                  LBSR CLRDIS     ; ETEINDRE AFFICHAGE
 E339 108CE2B5                CMPY #REGTBL-1  ; DEBUT TABLEAU DES REGISTRES?
 E33D 27E0                    BEQ FONREG      ; OUI, RETOUR FONCTION REGISTRE
 E33F 108CE2BD                CMPY #REGTBL+7  ; POINTE SUR PC?
 E343 271B                    BEQ REGPC       ; OUI, ALLUMER LE CONTENU
 E345 10BCE2B9                CMPY REGTBL+3   ; REGISTRES 8 BITS?
 E349 270B                    BEQ RE8BIT
 E34B 10BCE2B8                CMPY REGTBL+2
 E34F 230A                    BLS REBIT8
 E351 301C                    LEAX -4,X       ; REGISTRE 16 BITS X,Y,U,P,S
 E353 16FF7D                  LBRA TOUREG     ; VISUALISER CONTENU
 E356 301D            RE8BIT  LEAX -3,X       ; REGISTRES 8 BITS D,B,A,C
 E358 16FF78                  LBRA TOUREG     ; VISUALISE CONTENU
 E35B 301E            REBIT8  LEAX -2,X       ; REGISTRES 8 BITS B,A,C
 E35D 16FF73                  LBRA TOUREG     ; VISUALISER CONTENU
 E360 9EF8            REGPC   LDX <SAVPIL
 E362 300A                    LEAX 10,X       ; X POINTE SUR PC
 E364 16FF6C                  LBRA TOUREG


                      ****** PERMET DE CHANGER LE CONTENU D'UN REG LORS D'UN NMI ou SWI ******


 E367 318DFF4B        EXREGI  LEAY REGTBL,PCR
 E36B 9EF8                    LDX <SAVPIL
 E36D 17FCB0                  LBSR GETKEY     ; ALLUMER REG
 E370 8130                    CMPA #$30       ; REG DEMANDE?
 E372 1027FEC6                LBEQ RPOINT     ; OUI, CRUTER TOUCHES
 E376 8106                    CMPA #$06       ; CCR?
 E378 273B                    BEQ R8BREG      ; OUI
 E37A 3001                    LEAX 1,X
 E37C 3121                    LEAY 1,Y        ; REG SUIVANT
 E37E 8105                    CMPA #$05       ; ACCA?
 E380 2733                    BEQ R8BREG      ; OUI
 E382 3001                    LEAX 1,X
 E384 3121                    LEAY 1,Y
 E386 8104                    CMPA #$04       ; ACCB?
 E388 272B                    BEQ R8BREG      ; OUI
 E38A 3001                    LEAX 1,X
 E38C 3121                    LEAY 1,Y
 E38E 8103                    CMPA #$03       ; DPR?
 E390 2723                    BEQ R8BREG      ; OUI
 E392 3001                    LEAX 1,X
 E394 3121                    LEAY 1,Y
 E396 8102                    CMPA #$02       ; REG X?
 E398 2726                    BEQ REG16B      ; OUI
 E39A 3002                    LEAX 2,X
 E39C 3121                    LEAY 1,Y
 E39E 8101                    CMPA #$01       ; REG Y?
 E3A0 271E                    BEQ REG16B      ; OUI
 E3A2 3002                    LEAX 2,X
 E3A4 3121                    LEAY 1,Y 
 E3A6 8115                    CMPA #$15       ; REG U?
 E3A8 2716                    BEQ REG16B      ; OUI
 E3AA 3002                    LEAX 2,X
 E3AC 3121                    LEAY 1,Y
 E3AE 8114                    CMPA #$14       ; REG PCR?
 E3B0 270E                    BEQ REG16B      ; OUI
 E3B2 16FE87                  LBRA RPOINT
 E3B5 A6A4            R8BREG  LDA ,Y
 E3B7 97FF                    STA <DISBUF+5   ; AFFICHE TYPE DE REG
 E3B9 17FF3E                  LBSR AF2DIG     ; AFFICHE LE CONTENU DU REG 8BITS
 E3BC 301F                    LEAX -1,X
 E3BE 200B                    BRA CHANG8      ; CHANGER LE CONTENU
 E3C0 A6A4            REG16B  LDA ,Y
 E3C2 97FF                    STA <DISBUF+5   ; AFFICHE TYPE DE REG
 E3C4 17FF25                  LBSR SUITER+6   ; AFFICHE LE CONTENU DU REGISTRE 16BITS
 E3C7 301E                    LEAX -2,X
 E3C9 2015                    BRA CHAN16      ; CHANGER SON CONTENU
 E3CB 3430            CHANG8  PSHS Y,X        ; SAUVEGARDE DES POINTEURS
 E3CD 8E07FC                  LDX #DISBUF+2
 E3D0 108E0002                LDY #$02
 E3D4 1700EF                  LBSR CHTDON     ; SOUS PROG DE CHGT DES DONNEES
 E3D7 3530                    PULS Y,X
 E3D9 96E5                    LDA <PRESEH 
 E3DB A784                    STA ,X          ; NELLE DONNEE EN PILE
 E3DD 16FF3F                  LBRA FONREG     ; SCRUTER LES REGISTRES


 E3E0 3430            CHAN16  PSHS Y,X
 E3E2 8E07FA                  LDX #DISBUF
 E3E5 108E0006                LDY #$06
 E3E9 1700DA                  LBSR CHTDON
 E3EC 3530                    PULS Y,X
 E3EE DCE5                    LDD <PRESEH
 E3F0 ED84                    STD ,X
 E3F2 16FF2A                  LBRA FONREG


                      ****** AFFICHE VAL DU POST OCTET,EFFECTUE LA CONVERSION ******
                      ****** DECIMALE-HEXADECIMALE ET PLACE LE RESLT EN MEMRE ******


 E3F5 3001            AFIVAL  LEAX 1,X
 E3F7 3410                    PSHS X
 E3F9 17FC24                  LBSR GETKEY
 E3FC 8E07FA                  LDX #DISBUF
 E3FF 108E0006                LDY #$06
 E403 1700C0                  LBSR CHTDON
 E406 17FC17                  LBSR GETKEY
 E409 1700C0                  LBSR MSBDON
 E40C 0FE7                    CLR <PRELOW
 E40E D7E8                    STB <PRELOW+1
 E410 3510                    PULS X
 E412 17FC0B                  LBSR GETKEY
 E415 8131                    CMPA #$31
 E417 2703                    BEQ CALCON
 E419 16FD1C                  LBRA REMEMO
                              
 E41C 17FE7E          CALCON  LBSR CLRDIS
 E41F 1700EA                  LBSR DECHEX     ; CONVERSION DEC-HEXA
 E422 DCE7                    LDD <PRELOW
 E424 4D                      TSTA            ; A-0?
 E425 260B                    BNE TESTST      ; NON, VAL 16BITS?
 E427 58                      ASLB
 E428 251B                    BCS TSTSUI      ; VAL 16BITS?
 E42A 58                      ASLB
 E42B 2552                    BCS INTR08      ; VAL 16BITS?
 E42D 58                      ASLB
 E42E 254F                    BCS INTR08      ; VAL 8BITS?
 E430 201A                    BRA INTR05      ; VAL 5BITS?
                              
 E432 8180            TESTST  CMPA #$80       ; A>80?
 E434 2208                    BHI ERREUR      ; OUI, AFFICHE ERREUR
 E436 2702                    BEQ COMPAB
 E438 2066                    BRA INTR16      ; NON, VAL 16 BITS
                              
 E43A C100            COMPAB  CMPB #$00
 E43C 2762                    BEQ INTR16
 E43E 301F            ERREUR  LEAX -1,X
 E440 1F12            EREURE  TFR X,Y
 E442 160363                  LBRA CLIGNO
                              
 E445 56              TSTSUI  RORB
 E446 C180                    CMPB #$80       ; B>80?
 E448 2256                    BHI INTR16      ; OUI, VAL 16BITS
 E44A 2033                    BRA INTR08      ; NON, VAL 8BITS
 E44C 58              INTR05  ASLB
 E44D 2420                    BCC POSITIF
 E44F DCE7                    LDD <PRELOW
 E451 A684                    LDA ,X          ; POST DS A, VAL DS B
 E453 C110                    CMPB #$10       ; B>10?
 E455 2205                    BHI TSTPOC      ; OUI, TESTER POST OCTET
 E457 48                      ASLA
 E458 2407                    BCC POCPOS
 E45A 2023                    BRA INTR08
                              
 E45C 48              TSTPOC  ASLA
 E45D 24E1                    BCC EREURE      ; POST INCORRECT
 E45F 201E                    BRA INTR08      ; VAL 8BITS POSSIBLE
                              
 E461 08E3            POCPOS  ASL <PLUSMS     ; POSITIF?
 E463 241A                    BCC INTR08      ; VAL 8BITS
 E465 A684            CHPOST  LDA ,X
 E467 3404                    PSHS B
 E469 ABE0                    ADDA ,S+
 E46B A784                    STA ,X
 E46D 2025                    BRA STODN1
                              
 E46F DCE7            POSITIF LDD <PRELOW
 E471 A684                    LDA ,X
 E473 48                      ASLA
 E474 25CA                    BCS EREURE
 E476 08E3                    ASL <PLUSMS
 E478 24EB                    BCC CHPOST      ; SI POSITIF , CHARGER
 E47A 50                      NEGB
 E47B C41F                    ANDB #$1F
 E47D 20E6                    BRA CHPOST      ; SI NEG, CHARGER
                              
 E47F DCE7            INTR08  LDD <PRELOW
 E481 A680                    LDA ,X+
 E483 49                      ROLA
 E484 24B8                    BCC ERREUR      ; SI DIFFERENT 8BITS ERREUR
 E486 46                      RORA
 E487 840F                    ANDA #$0F
 E489 8108                    CMPA #$08       ; POST OCTET 8BITS
 E48B 26B1                    BNE ERREUR
 E48D 08E3                    ASL <PLUSMS     ; OUI, POSITIF?
 E48F 2409                    BCC STADON      ; OUI STOCKER DONNEE
 E491 50                      NEGB
 E492 E784            STODON  STB ,X
 E494 17034E          STODN1  LBSR AFFIAD+2
 E497 16FC9E                  LBRA REMEMO
 E49A C180            STADON  CMPB #$80
 E49C 27A0            RCR     BEQ ERREUR
 E49E 20F2                    BRA STODON
                              
 E4A0 A680            INTR16  LDA ,X+
 E4A2 49                      ROLA
 E4A3 2499                    BCC ERREUR      ; DIFF 16BITS
 E4A5 46                      RORA
 E4A6 840F                    ANDA #$0F
 E4A8 8109                    CMPA #$09       ; 16BITS?
 E4AA 2692                    BNE ERREUR
 E4AC DCE7                    LDD <PRELOW
 E4AE 08E3                    ASL <PLUSMS     ; POSITIF?
 E4B0 2409                    BCC COMPAR      ; OUI
 E4B2 43                      COMA
 E4B3 53                      COMB
 E4B4 C30001                  ADDD #$01
 E4B7 ED84            CHAR16  STD ,X
 E4B9 20D9                    BRA STODN1
 E4BB 10838000        COMPAR  CMPD #$8000
 E4BF 27DB                    BEQ RCR
 E4C1 20F4                    BRA CHAR16


                      ****** CHANGEMENT DE CONTENU DES REGISTRES PRESERVES EN PILE ******


 E4C3 17FB5A          CHTCHI  LBSR GETKEY     ; SCRUTER CHIFFRES
 E4C6 338D000D        CHTDON  LEAU >INTVAL,PCR
 E4CA 3440                    PSHS U
 E4CC 17FC20          MSBDON  LBSR HEXCON     ; CONVERTI CHIFFRE EN HEXA
 E4CF 1F89                    TFR A,B
 E4D1 17FC34                  LBSR R7SEG
 E4D4 A780                    STA ,X+         ; ENVOI CHIFFRE SUR AFFICHEUR
 E4D6 39                      RTS
                              
 E4D7 3404            INTVAL  PSHS B
 E4D9 313F                    LEAY -1,Y
 E4DB 1F20                    TFR Y,D         ; 1 OU 5 OU 3
 E4DD 54                      LSRB 
 E4DE 251A                    BCS DEPLAC
 E4E0 108C0002        SUITEP  CMPY #$02       ; DISBUF+0
 E4E4 270D                    BEQ CAL16       ; REGISTRE 16BITS
 E4E6 1F20                    TFR Y,D         ; 0 OU 4
 E4E8 54                      LSRB
 E4E9 2419                    BCC CALCU8      ; REGISTRE 8BITS
 E4EB 108C0000        SUI     CMPY #$00
 E4EF 2ED2                    BGT CHTCHI      ; CHIFFRE SUIVANT
 E4F1 2706                    BEQ RETOUR
 E4F3 3502            CAL16   PULS A
 E4F5 ABE0                    ADDA ,S+
 E4F7 97E6                    STA <PRESEL
 E4F9 39              RETOUR  RTS

 E4FA 3502            DEPLAC  PULS A
 E4FC 48                      ASLA
 E4FD 48                      ASLA
 E4FE 48                      ASLA
 E4FF 48                      ASLA
 E500 3402                    PSHS A
 E502 20DC                    BRA SUITEP
                              
 E504 3502            CALCU8  PULS A
 E506 ABE0                    ADDA ,S+
 E508 97E5                    STA <PRESEH
 E50A 20DF                    BRA SUI


                      ****** SOUS-PROG DE CONVERSION DECIMALE-HEXA, RETOUR EN FCT MEM ******


 E50C 3438            DECHEX  PSHS Y,X,DP
 E50E 860A                    LDA #$0A
 E510 1F8B                    TFR A,DP        ; 10 DS DP
 E512 8E07E6                  LDX #PRESEL
 E515 108E0000                LDY #$00
 E519 A684            CALCUL  LDA ,X
 E51B 3121                    LEAY 1,Y
 E51D 108C0001                CMPY #$01
 E521 260B                    BNE COMPR2
 E523 8D66                    BSR LSBDSA      ; LSB DANS ACCA
 E525 3D              MULPAR  MUL
 E526 F307E7                  ADDD >PRELOW    ; D+PRELOW DS D
 E529 FD07E7                  STD >PRELOW
 E52C 20EB                    BRA CALCUL
                              
 E52E 108C0002        COMPR2  CMPY #$02
 E532 2606                    BNE COMPR3
 E534 8D6D                    BSR MUL100      ; MUL DES CENTAINES
 E536 8D62                    BSR MSBLSB
 E538 20EB                    BRA MULPAR
                              
 E53A 108C0003        COMPR3  CMPY #$03
 E53E 260C                    BNE COMPR4
 E540 8D61                    BSR MUL100      ; MUL DES MILLIERS
 E542 A684                    LDA ,X
 E544 8D45                    BSR LSBDSA
 E546 3D              MULFIN  MUL
 E547 B607E1                  LDA >PRESER
 E54A 20D9                    BRA MULPAR
                              
 E54C 108C0004        COMPR4  CMPY #$04
 E550 2623                    BNE COMPR5
 E552 8602                    LDA #$02
 E554 B707E2                  STA >SAUVER
 E557 8D50            RECMCE  BSR MUP100      ; MUL DES DIZAINES DE MILLIERS
 E559 7A07E2                  DEC >SAUVER
 E55C 2705                    BEQ M10000
 E55E F707E1                  STB >PRESER
 E561 20F4                    BRA RECMCE
                              
 E563 8D35            M10000  BSR MSBLSB
 E565 8103            SECPAR  CMPA #$03
 E567 2406                    BHS TESTER
 E569 108E0005                LDY #$05
 E56D 20D7                    BRA MULFIN
 E56F 2211            TESTER  BHI AFEROR
 E571 8602                    LDA #$02
 E573 20D1                    BRA MULFIN
                              
 E575 108C0005        COMPR5  CMPY #$05
 E579 260E                    BNE FINCAL
 E57B 8601                    LDA #$01
 E57D F607E1                  LDB >PRESER
 E580 20E3                    BRA SECPAR
                          
 E582 3578            AFEROR  PULS DP,X,Y,U
 E584 1F12                    TFR X,Y
 E586 16021F                  LBRA CLIGNO
                              
 E589 35B8            FINCAL  PULS PC,X,Y,DP      ; RTS

 E58B C608            LSBDSA  LDB #$08
 E58D 48              DECENC  LSLA
 E58E 5A                      DECB
 E58F C104                    CMPB #$04
 E591 26FA                    BNE DECENC
 E593 44              DECTJS  LSRA
 E594 5A                      DECB
 E595 26FC                    BNE DECTJS
 E597 1FB9                    TFR DP,B
 E599 39                      RTS

 E59A A684            MSBLSB  LDA ,X
 E59C 44                      LSRA
 E59D 44                      LSRA
 E59E 44                      LSRA
 E59F 44                      LSRA
 E5A0 301F                    LEAX -1,X
 E5A2 39                      RTS

 E5A3 338D0008        MUL100  LEAU >INTER3,PCR
 E5A7 3440                    PSHS U
 E5A9 1FB9            MUP100  TFR DP,B
 E5AB 1F98                    TFR B,A
 E5AD 3D                      MUL
 E5AE 39                      RTS
                              
 E5AF F707E1          INTER3  STB >PRESER
 E5B2 39                      RTS


                      ****** RECONNAISSANCE DU BIT DU CARACTERE TRANSMIS ******
                      *
                      *
                      *
 E5B3 3406            RECBIT  PSHS B,A
 E5B5 C606            NOUDLY  LDB #$06
 E5B7 B6A005          SCARRY  LDA DISCNT      ; CHARGER PB7 PIA
 E5BA 48                      LSLA            ; PB7=1
 E5BB 24FA                    BCC SCARRY      ; NON, ATTENDRE UN 1 DEBUT CARAC.
 E5BD 5A              DLY30U  DECB            ; OUI, DELAI # 30MICROSEC
 E5BE 26FD                    BNE DLY30U
 E5C0 B6A005                  LDA DISCNT      ; CHARGER DE NOUVEAU PB7
 E5C3 48                      LSLA            ; 1 TOUJOURS PRESENT?
 E5C4 24EF                    BCC NOUDLY      ; NON, NOUVELLE ATTENTE D'UN CARAC.
 E5C6 C624                    LDB #$24        ; OUI, DELAI 430MICROSEC MINI
 E5C8 5A              CARRY1  DECB
 E5C9 B6A005                  LDA DISCNT
 E5CC 48                      LSLA            ; TOUJOURS 1?
 E5CD 25F9                    BCS CARRY1      ; OUI, DECOMPTER
 E5CF 86EF                    LDA #$EF        ; NON, DECOMPTE>720MICROSEC.
 E5D1 5D                      TSTB            ; BIT CARAC.=0?
 E5D2 2B01                    BMI AFSIGN      ; OUI, AFFICHER SIGNE POUR 0
 E5D4 47                      ASRA            ; NON, AFFICHER SIGNE POUR 1
 E5D5 B7A004          AFSIGN  STA DISREG
 E5D8 5D                      TSTB
 E5D9 3586                    PULS PC,B,A     ; RTS


                      ******** POSITIONNEMENT DU BIT DU CARACTERE ********
                      ***** TRANSMIS DANS LE LSB DE L'ACCUMULATEUR B *****
                      *
                      *
                      *
 E5DB 3402            BITLSB  PSHS A
 E5DD 8DD4            BITCA1  BSR RECBIT      ; RECONNAISSANCE BIT TRANSMIS
 E5DF 2AFC                    BPL BITCA1      ; BIT CARAC. = 1
 E5E1 8608                    LDA #$08        ; RECONNAISSANCE BIT SUIVANTS
 E5E3 8DCE            CONROT  BSR RECBIT      ; BIT CARAC=0 RECON BITS SUIVANTS
 E5E5 1CFE                    ANDCC #$FE      ; CARRY=0
 E5E7 2B02                    BMI BITCA0      ; BIT CARACTERE = 0 ?
 E5E9 1A01                    ORCC #$01       ; NON, METRE CARRY A 1
 E5EB 56              BITCA0  RORB            ; OUI, DEPLACER LA CARRY
 E5EC 4A                      DECA            ; PAR ROTATIONS SUCCESSIVES
 E5ED 26F4                    BNE CONROT      ; CARRY DANS LSB DE ACCB
 E5EF 3582                    PULS PC,A       ; RTS


                      ****** CHARGEMENT D'UN PROGRAMMA PROVENANT D'UNE CASSETTE ******
                      ****************************************************************


 E5F1 CC0000          EXLOAD  LDD #$0000
 E5F4 FDA006                  STD SCNREG      ; ACCES A DDRAB
 E5F7 CCFF7F                  LDD #$FF7F
 E5FA FDA004                  STD DISREG      ; PA ET PB EN SORTIE
 E5FD CC0404                  LDD #$0404      ; PB7 EN ENTREE
 E600 FDA006                  STD SCNREG      ; ACCES DRAB
 E603 CCFF04                  LDD #$FF04      ; ETEINDRE LES AFFICHEURS
 E606 FDA004                  STD DISREG      ; ET SELECTIONNER LE 1er DIGIT
 E609 8DD0            DETECS  BSR BITLSB      ; DETECTE CARACTERE DEBUT CHARGEMENT
 E60B C153                    CMPB #$53       ; CARACTERE S TRANSMIS ?
 E60D 26FA                    BNE DETECS      ; NON, CONTINUER A CHERCHER CARACTERE
 E60F 8DCA                    BSR BITLSB      ; OUI, CARACTERES SUIVANTS 
 E611 C131                    CMPB #$31       ; CARACTERE 1 TRANSMIS ?
 E613 2708                    BEQ CARSUI      ; OUI, CARACTERES SUIVANTS
 E615 C14A                    CMPB #$4A       ; NON, CARACTERE FIN = J ?
 E617 26F0                    BNE DETECS      ; NON, DETECTER CARACTERE DE FIN
 E619 8669            AFICHA  LDA #$69        ; LDA #$69 OUI, AFFICHER FIN DU CHARGEMENT
 E61B 2032                    BRA DISFIN


                      ****** MISE EN MEMOIRE, POINTEE PAR X DES CARAC TRANSMIS ******
                      ***************************************************************

 E61D 8DBC            CARSUI  BSR BITLSB      ; CONVERSION INTERVALE
 E61F D7F0                    STB <SAVCNT
 E621 96F0                    LDA <SAVCNT
 E623 8DB6                    BSR BITLSB
 E625 D7F6                    STB <SAVB1
 E627 3404                    PSHS B
 E629 ABE0                    ADDA ,S+        ; A + B DANS ACCA
 E62B 0AF0                    DEC <SAVCNT 
 E62D 8DAC                    BSR BITLSB 
 E62F D7F7                    STB <SAVB2
 E631 3404                    PSHS B
 E633 ABE0                    ADDA ,S+        ; A + B DANS ACCA
 E635 9EF6                    LDX <SAVB1      ; X CONTIENT ADRESSE DE CHARGEMENT
 E637 0AF0            SUICHA  DEC <SAVCNT
 E639 270A                    BEQ DERADD      ; DERNIERE ADRESSE ?
 E63B 8D9E                    BSR BITLSB      ; NON, CONTINUER A CHARGER
 E63D E780                    STB ,X+         ; ET A METTRE EN MEMOIRE
 E63F 3404                    PSHS B
 E641 ABE0                    ADDA ,S+        ; A + B DANS ACCA
 E643 20F2                    BRA SUICHA 
                              
 E645 8D94            DERADD  BSR BITLSB      ; OUI, DERNIERE ADRESSE
 E647 3404                    PSHS B
 E649 ABE0                    ADDA ,S+        ; ERREUR DANS LA TRANSMISSION
 E64B 27BC                    BEQ DETECS 
 E64D 8678                    LDA #$78        ; OUI, AFFICHER L'ERREUR
 E64F 17FC4B          DISFIN  LBSR CLRDIS 
 E652 97FA                    STA <DISBUF
 E654 17FA24          BOUFIN  LBSR DISPRE
 E657 20FB                    BRA BOUFIN


                      ****** CHARGEMENT D'UN PROGRAMME D'UNE ZONE MEMOIRE ******
                      *******************VERS UN MAGNETOCASSETTE ***************
                      **********************************************************


 E659 8E003D          EXPUNC  LDX #$003D 
 E65C 9FFE                    STX <DISBUF+4   ; AFFICHER S POUR START
 E65E 17FA5C                  LBSR BADDR      ; FABRIQUER ADRESSE DE DEBUT
 E661 9FF2                    STX <SADDR1     ; SAUVE DEBUT ADRESSE
 E663 8669                    LDA #$69        ; AFFICHER F POUR FIN
 E665 97FF                    STA <DISBUF+5
 E667 17FA53                  LBSR BADDR      ; FABRIQUER ADRESSE DE FIN
 E66A 9FF4                    STX <SADDR2     ; SAUVE FIN ADRESSE
 E66C 4F                      CLRA            ; ACCES A DDRB
 E66D B7A007                  STA SCNCNT
 E670 4A                      DECA            ; PB EN SORTIE
 E671 B7A005                  STA DISCNT 
 E674 8604                    LDA #$04        ; ACCES A ORB DU PIA
 E676 B7A007                  STA SCNCNT
 E679 4F                      CLRA            ; LIGNE No 0 A 0 ET PB6 = 0
 E67A B7A005                  STA DISCNT
 E67D 86FF                    LDA #$FF 
 E67F 8D71                    BSR DLY5MS      ; FABRICATION D'UNE SERIE
 E681 8D6F                    BSR DLY5MS      ; DE 10 PERIODES DE 1400 ET 5600HZ
 E683 8D63            RECYCL  BSR DEBCHA      ; CHARGER CARACTERE DE DEBUT
 E685 86FF                    LDA #$FF
 E687 D6F3                    LDB <SADDR1+1   ; POIDS FAIBLE ADRESSE DEBUT
 E689 3404                    PSHS B
 E68B A0E0                    SUBA ,S+        ; A + B DANS ACCA
 E68D 840F            CODFIN  ANDA #$0F 
 E68F 8B03                    ADDA #$03       ; TRANSMISSION D'UN CARACTERE
 E691 8D5F                    BSR DLY5MS      ; INTERVALE
 E693 97F1                    STA <SAVREG
 E695 8E07F2                  LDX #SADDR1     ; DEBUT ADRESSE
 E698 8D56                    BSR TRANSM      ; TRANSMISSION MSB ADRESSE DEBUT
 E69A 9BF1                    ADDA <SAVREG
 E69C 97F1                    STA <SAVREG
 E69E 3001                    LEAX 1,X
 E6A0 8D4E                    BSR TRANSM      ; TRANSMISSION LSB ADRESSE DEBUT
 E6A2 9BF1                    ADDA <SAVREG
 E6A4 97F1                    STA <SAVREG
 E6A6 9EF2                    LDX <SADDR1     ; X POINTE SUR PROGRAMME A ENREGISTRER
 E6A8 8D46            TJTRAN  BSR TRANSM      ; TRANSMISSION DES DONNEES
 E6AA 9BF1                    ADDA <SAVREG
 E6AC 97F1                    STA <SAVREG 
 E6AE 9CF4                    CMPX <SADDR2    ; FIN D'ENREGISTREMENT ?
 E6B0 270A                    BEQ FINENR      ; OUI, TRANSMETTRE FIN
 E6B2 3001                    LEAX 1,X        ; NON, DONNEE SUIVANTE A TRANSMETTRE
 E6B4 9FF2                    STX <SADDR1     ; SAUVE POINTEUR
 E6B6 96F3                    LDA <SADDR1+1
 E6B8 850F                    BITA #$0F       ; 16 CARACTERES TRANSMIS ?
 E6BA 26EC                    BNE TJTRAN      ; NON, CONTINUER TRANSMISSION
 E6BC 96F1            FINENR  LDA <SAVREG     ; OUI, 16 CARACTERES TRANSMIS
 E6BE 40                      NEGA
 E6BF 8D31                    BSR DLY5MS      ; TRANSMISSION CODE POUR 16CARAC TRANSMIS
 E6C1 9CF4                    CMPX <SADDR2    ; FIN PROGRAMME ?
 E6C3 2718                    BEQ FINCHA      ; OUI, TRANSMETTRE FIN CHARGEMENT
 E6C5 96F2                    LDA <SADDR1     ; NON, MSB DEBUT ADRESSE
 E6C7 91F4                    CMPA <SADDR2    ; < MSB FIN ADRESSE ?
 E6C9 2BB8                    BMI RECYCL      ; OUI, RECOMMENCER CYCLE
 E6CB D6F3                    LDB <SADDR1+1   ; NON, LSB DEBUT ADRESSE
 E6CD 96F5                    LDA <SADDR2+1   ; -LSB FIN ADRESSE <>0 ?
 E6CF 84F0                    ANDA #$F0
 E6D1 3404                    PSHS B
 E6D3 A1E0                    CMPA ,S+        ; COMPARE B A ACCA
 E6D5 26AC                    BNE RECYCL      ; OUI, RECOMMENCER UN CYCLE
 E6D7 8D0F                    BSR DEBCHA      ; NON, TRANSMETTRE DEBUT CHARGEMENT
 E6D9 96F5                    LDA <SADDR2+1   ; DU CODE DE FIN
 E6DB 20B0                    BRA CODFIN      ; CHARGEMENT PUIS FIN


                      ****** FABRICATION DU SIGNAL DE FIN DE CHARGEMENT ******
                      *
                      *
 E6DD 8653            FINCHA  LDA #$53
 E6DF 8D11                    BSR DLY5MS
 E6E1 864A                    LDA #$4A
 E6E3 8D0D                    BSR DLY5MS
 E6E5 16FF31                  LBRA AFICHA     ; AFFICHAGE FIN DE CHARGEMENT


                      ****** FABRICATION DU SIGNAL DE DEBUT DE CHARGEMENT ******
                      *
                      *
 E6E8 8653            DEBCHA  LDA #$53
 E6EA 8D06                    BSR DLY5MS      ; TRANSMISSION CARACTERE S
 E6EC 8631                    LDA #$31
 E6EE 2002                    BRA DLY5MS      ; TRANSMISSION DU CARACTERE 1


                      ****** FABRICATION DU SIGNAL SERIALISE ******
                      *********** DE 10 PERIODES PAR DONNEES **********
                      *
                      *
 E6F0 A684            TRANSM  LDA ,X
 E6F2 3406            DLY5MS  PSHS B,A
 E6F4 C60A                    LDB #$0A
 E6F6 D7EF                    STB <SAVRES
 E6F8 1CFE                    ANDCC #$FE      ; METTRE CARRY A 0
 E6FA C690            BOUCL3 LDB #$90         ; DELAI = 720MICROSEC
 E6FC 2402                    BCC SAUT
 E6FE C624                    LDB #$24        ; DELAI = 180MICROSEC
 E700 3404            SAUT    PSHS B
 E702 C640                    LDB #$40
 E704 F7A005                  STB DISCNT      ; PB6=1
 E707 E6E4                    LDB ,S
 E709 5A              BOUCL1  DECB            ; DELAY 180 OU 720MICROSEC
 E70A 26FD                    BNE BOUCL1
 E70C F7A005                  STB DISCNT      ; PB6=0
 E70F 3504                    PULS B
 E711 5A              BOUCL2  DECB            ; NOUVEAU DELAY DE 180 OU 720MICROSEC
 E712 26FD                    BNE BOUCL2
 E714 1A01                    ORCC #$01       ; METTRE CARRY A 1
 E716 46                      RORA
 E717 0AEF                    DEC <SAVRES
 E719 26DF                    BNE BOUCL3      ; TERMINER DELAY DE 5MS
 E71B 3586                    PULS PC,B,A     ; RTS


                      ***** EXECUTION DE LA FONCTION OFFSET *****
                      *
                      *
                      *
 E71D 17FA4B          EXOFST  LBSR INCREM
 E720 A684                    LDA ,X
 E722 8116                    CMPA #$16       ; LBRA?
 E724 2734                    BEQ EXLBIL      ; OUI, BRANCHEMENT LONG INCONDITIONNEL
 E726 8117                    CMPA #$17       ; LBSR/
 E728 2730                    BEQ EXLBIL      ; OUI, //////////////////////////////
 E72A A682                    LDA ,-X         ; POINTER SUR INSTRUCTION PRECEDENTE
 E72C 8110                    CMPA #$10       ; BRANCHEMENT CONDITIONNEL LONG?
 E72E 2728                    BEQ EXLBCL      ; OUI
 E730 3003            OFPOCT  LEAX 3,X        ; X CONTIENT ADRESSE DU BRANCHEMENT COURT
 E732 8D3B                    BSR CALOFS      ; CALCULER OFFSET
 E734 2951                    BVS RETOU1      ; SI DEPASSEMENT DE CAPACITE, ERREUR
 E736 95E9                    BITA <NEGPOS
 E738 2704                    BEQ BRANPO      ; TOUT MSB A 0 = BRANCH POS
 E73A 2B09                    BMI BRANNE      ; MSBIT=1 BRANCH NEG
 E73C 2049                    BRA RETOU1      ; BRANCH SUR 16 BITS
                              
 E73E 5D              BRANPO  TSTB
 E73F 2746                    BEQ RETOU1      ; BRANCH NUL IMPOSSIBLE 
 E741 2B44                    BMI RETOU1      ; N=1, ERREUR BRANCH=16BITS
 E743 200B                    BRA CHARGE      ; N=0, CHARGE OFFSET DS MEM PROG
                              
 E745 91E9            BRANNE  CMPA <NEGPOS    ; A=FF?
 E747 263E                    BNE RETOU1      ; NON, ERREUR BRANCH SUR 16 BITS
 E749 5D                      TSTB            ; POSITIONNER N
 E74A 2A3B                    BPL RETOU1      ; N=0, BRANCH SUR 16BITS
 E74C C1FF                    CMPB #$FF
 E74E 2737                    BEQ RETOU1
 E750 E7A2            CHARGE  STB ,-Y         ; STOCKE OFFSET DS MEM PROG
 E752 17008E                  LBSR AFFIAD     ; AFFICHE ADRESSE DE STOCKAGE
 E755 16F9E0                  LBRA REMEMO     ; RETOUR DS FCT MEMOIRE
                              
 E758 3001            EXLBCL  LEAX 1,X 
 E75A 3003            EXLBIL  LEAX 3,X        ; BRANCH LONG, X=ADRESSE DE DEPART
 E75C 8D11                    BSR CALOFS      ; CALCULE OFFSET
 E75E 292B                    BVS RETOU2      ; SI DEPASSE CAPACITE, ERREUR
 E760 81FF                    CMPA #$FF
 E762 2717                    BEQ PLUCOU      ; BRANCH COURT PEUT ETRE POSSIBLE
 E764 8100                    CMPA #$00 
 E766 2719                    BEQ BCOURT      ; BRANCH>0 COURT POSSIBLE
 E768 EDA3            SUITEB  STD ,--Y        ; STOCKE OFFSET DS MEM PROG
 E76A 8D77                    BSR AFFIAD      ; AFFICHE ADRESSE
 E76C 16F9C9                  LBRA REMEMO     ; RETOUR DS FCT MEMOIRE


                      ****** CALCULE L'OFFSET SUR 16 BITS ******
                      **** D CONTIENT L'OFFSET,Y=ADRESSE DEP ***
                      *
                      *
                      *
 E76F 1F12            CALOFS  TFR X,Y         ; Y=ADRESSE DE DEPART
 E771 109FEA                  STY <ADDDEP
 E774 8D4C                    BSR AFFIAR      ; AFFICHAGE ADRESSE ARRIVEE
 E776 1F10                    TFR X,D         ; D=ADRESSE DEPART
 E778 93EA                    SUBD <ADDDEP    ; D=ARRIVEE-DEPART=OFFSET
 E77A 39                      RTS


                      ** DETERMINE SI LE BRANCH SUR 16BITS PEUT SE FAIRE SUR 8BITS **
                      *
                      *
                      *
 E77B 59              PLUCOU  ROLB
 E77C 250D                    BCS RETOU2      ; BRANCH COURT POSSIBLE
 E77E 56                      RORB
 E77F 20E7                    BRA SUITEB      ; CONTINUE CALCUL
                              
 E781 59              BCOURT  ROLB
 E782 2407                    BCC RETOU2
 E784 56                      RORB
 E785 20E1                    BRA SUITEB
                          
 E787 313E            RETOU1  LEAY -2,Y
 E789 201D                    BRA CLIGNO      ; AFFICHE ERREUR, PUIS RETOUR MEMORY
                              
 E78B 313C            RETOU2  LEAY -4,Y
 E78D A6A0                    LDA ,Y+
 E78F 8110                    CMPA #$10
 E791 2713                    BEQ CLIGNE
 E793 A6A4                    LDA ,Y
 E795 8116                    CMPA #$16
 E797 270F                    BEQ CLIGNO
 E799 8117                    CMPA #$17
 E79B 270B                    BEQ CLIGNO
 E79D 848F                    ANDA #$8F
 E79F 818D                    CMPA #$8D
 E7A1 2705                    BEQ CLIGNO
 E7A3 16FA96                  LBRA RPOINT     ; SI PAS BRANCH, RETOUR RESET
                              
 E7A6 313F            CLIGNE  LEAY -1,Y
 E7A8 8D39            CLIGNO  BSR AFFIAD      ; AFFICHE ADRESSE OU EST L'ERREUR
 E7AA 8D09                    BSR ERROR       ; PLACE ERREUR DS DISBUF
 E7AC 108E02FF                LDY #$02FF
 E7B0 17F8C8          AFCLIG  LBSR DISPRE     ; AFFICHE L'ERREUR
 E7B3 2006                    BRA TEMPO       ; TEMPORAIREMENT
                              
 E7B5 CC7941          ERROR   LDD #$7941      ; PLACE ER DANS LES 2
 E7B8 DDFE                    STD <DISBUF+4   ; DERNIERS DIGITS
 E7BA 39                      RTS
                              
 E7BB 313F            TEMPO   LEAY -1,Y
 E7BD 26F1                    BNE AFCLIG
 E7BF 16F976                  LBRA REMEMO     ; RETOUR DANS FCT MEMORY


                      ****** AFFICHAGE DE L'ADRESSE D'ARRIVEE ******
                      ** STOCKE DS NEGPOS LE SENS DE L'OFFSET<>0 ***
                      *
                      *
                      *
 E7C2 CC6F41          AFFIAR  LDD #$6F41      ; AFFICHE AR DS DISBUF
 E7C5 DDFE                    STD <DISBUF+4
 E7C7 17F8F3                  LBSR BADDR      ; AFFICHE ADRESSE ARRIVEE
 E7CA 9FEC                    STX <ADDARR
 E7CC 17F851                  LBSR GETKEY
 E7CF 8131                    CMPA #$31       ; TOUCHE GO?
 E7D1 2703                    BEQ SUIVT       ; OUI, CHARGE ADRESSE
 E7D3 16FA66                  LBRA RPOINT 
                              
 E7D6 17FAC4          SUIVT   LBSR CLRDIS     ; ETEINDRE AFFICHAGE
 E7D9 86FF                    LDA #$FF
 E7DB 9CEA                    CMPX <ADDDEP    ; ARRIVEE<DEPART ?
 E7DD 2501                    BLO STOCKA      ; OUI, STOCKE DS NEGPOS
 E7DF 4C                      INCA
 E7E0 97E9            STOCKA  STA <NEGPOS     ; NEGPOS=0>0 NEGPOS=FF<0
 E7E2 39                      RTS


                      ****** AFFICHE L'ADRESSE DE CHARGEMENT ******
                      ** DE L'OFFSET DANS LES 4 PREMIERS DIGITS ***
                      *
                      *
                      *
 E7E3 1F21            AFFIAD  TFR Y,X
 E7E5 1F10                    TFR X,D         ; NLLE ADRESSE DE DEP APRES INSTRUC
 E7E7 17F91E                  LBSR R7SEG 
 E7EA 97FB                    STA <DISBUF+1
 E7EC 1F10                    TFR X,D
 E7EE 17F913                  LBSR L7SEG
 E7F1 97FA                    STA <DISBUF
 E7F3 1F98                    TFR B,A
 E7F5 17F910                  LBSR R7SEG
 E7F8 97FD                    STA <DISBUF+3
 E7FA 1F98                    TFR B,A
 E7FC 17F905                  LBSR L7SEG
 E7FF 97FC                    STA <DISBUF+2
 E801 39                      RTS


                      *************************
                      ****** NMI ROUTINE ******
                      *************************


 E802 BE07DD          RNMI    LDX >SAVNMI
 E805 6E84                    JMP ,X          ; EXECUTE ROUTINE NMI



                      *******************************        
                      * Routines from documentation *
                      *******************************

                      * Displays: 6809 uP
                      * Etudes atour du 6809, p55
                                            
                      D6809uP                            
 E807 CC7D7F                  LDD     #$7D7F      ; 68
 E80A DDFA                    STD     <$07FA
 E80C CC7E3F                  LDD     #$7E3F      ; 09
 E80F DDFC                    STD     <$07FC
 E811 CC636B                  LDD     #$636B      ; uP
 E814 DDFE                    STD     <$07FE
 E816 17F862          DPLOOP  LBSR    >DISPRE
 E819 20FB                    BRA     DPLOOP

                      KBTEST
 E81B 8607                    LDA     #$07
 E81D 1F8B                    TFR     A, DP
 E81F CCFFFF                  LDD     #$FFFF
 E822 DDFA                    STD     <$07FA
 E824 DDFC                    STD     <$07FC
 E826 DDFE                    STD     <$07FE
 E828 17F850                  LBSR     DISPRE
 E82B 20EE                    BRA     KBTEST



                      *************************************
                      ****** VECTEURS D'INTERRUPTION ******
                      *************************************


                              ORG $EFF2

 EFF2 077A                    FDB RSWI3       ; INTERRUPTION SOFT No 3
 EFF4 E291                    FDB RSWI2       ; INTERRUPTION SOFT No 2
 EFF6 0775                    FDB RFIRQ       ; INTERRUPTION RAPIDE HARD
 EFF8 0770                    FDB RIRQ        ; INTERRUPTION NORMALE HARD
 EFFA E2BF                    FDB RSWI        ; INTERRUPTION SOFT No 1
 EFFC E802                    FDB RNMI        ; INTERRUPTION NON MASQUABLE HARD
 EFFE E22E                    FDB RESTAR      ; RESET GENERAL


                      ***************************************
                      ****** EMPLACEMENT DES REGISTRES ******
                      ***************************************


 07C0                 PILE    EQU $07C0       ; PILE SYSTEME
 07A0                 PILMON  EQU $07A0       ; INITIALISATION PILE MONITEUR
 077A                 RSWI3   EQU $077A
 0775                 RFIRQ   EQU $0775
 0770                 RIRQ    EQU $0770
 0760                 MABS    EQU $0760       ; P-segment for DISBUF+4 & DISBUF+5

                      *       A2 A1 A0  CRA-2 CRB-2 
                      *  0-3  0  x  x    x     x   not selected
                      *  4    1  0  0    0     x   Data Direction Register A
                      *  4    1  0  0    1     x   Peripheral Register A
                      *  5    1  0  1    x     0   Data Direction Register B
                      *  6    1  1  0    x     x   Control Register A
                      *  7    1  0  1    x     1   Peripheral Register B
                      *  7    1  1  1    x     x   Control Register B
                      * 
                      * Control Register
                      * b7  b6  b5  b4  b3  b2  b1  b0
                      *  |   |   |   |   |   |   |   |
                      *  |   |   |   |   |   |   |   +--- Interrupt Request Enable: b0=1  / Disable b0=1
                      *  |   |   |   |   |   |   +------- Cx1 Active transition: b1=high-to-low / b1=low-to-high
                      *  |   |   |   |   |   +----------- DDR/Output Register selected: b2=0 DDR / b2=1 Output 
Register
                      *  |   |   +---+---+--------------- Cx2 Mode
                      *  |   +--------------------------- Interrupt Flag Cx2: b7=1 Interrupt occurred
                      *  +------------------------------- Interrupt Flag Cx1: b7=1 Interrupt occurred

 A004                 DISREG  EQU $A004       ; DRA-ACCES CLAVIER ET SEGMENTS      PIA DDR/PR A
 A005                 DISCNT  EQU $A005       ; ORB-COMMANDE CLAVIER ET AFFICHEUR  PIA DDR/PR B
 A006                 SCNREG  EQU $A006       ; PIACRA
 A007                 SCNCNT  EQU $A007       ; PIACRB


                      ************************************************
                      ****** PARAMETRES VARIABLES SITUES EN RAM ******
                      ************************************************

                              ORG $07DD

 07DD                 SAVNMI  RMB 2
 07DF                 SASWI2  RMB 2
 07E1                 PRESER  RMB 1
 07E2                 SAUVER  RMB 1
 07E3                 PLUSMS  RMB 1
 07E4                 SAVPOC  RMB 1
 07E5                 PRESEH  RMB 1
 07E6                 PRESEL  RMB 1
 07E7                 PRELOW  RMB 2
 07E9                 NEGPOS  RMB 1
 07EA                 ADDDEP  RMB 2
 07EC                 ADDARR  RMB 2
 07EE                 COMDEC  RMB 1
 07EF                 SAVRES  RMB 1
 07F0                 SAVCNT  RMB 1
 07F1                 SAVREG  RMB 1
 07F2                 SADDR1  RMB 2
 07F4                 SADDR2  RMB 2
 07F6                 SAVB1   RMB 1
 07F7                 SAVB2   RMB 1
 07F8                 SAVPIL  RMB 2
 07FA                 DISBUF  RMB 6

                              END 

SYMBOL TABLE
    ADDARR 02 07EC    ADDDEP 02 07EA    AF2DIG 02 E2FA    AFCLIG 02 E7B0
    AFEROR 02 E582    AFFIAD 02 E7E3    AFFIAR 02 E7C2    AFICHA 02 E619
    AFIVAL 02 E3F5    AFSIGN 02 E5D5     BADDR 02 E0BD    BCOURT 02 E781
    BITCA0 02 E5EB    BITCA1 02 E5DD    BITLSB 02 E5DB    BOUCL1 02 E709
    BOUCL2 02 E711    BOUCL3 02 E6FA    BOUFIN 02 E654    BPOINT 02 E20D
    BRANNE 02 E745    BRANPO 02 E73E     CAL16 02 E4F3    CALCON 02 E41C
    CALCU8 02 E504    CALCUL 02 E519    CALOFS 02 E76F    CALPOT 02 E1E8
    CARRY1 02 E5C8    CARSUI 02 E61D    CHAN16 02 E3E0    CHANG8 02 E3CB
    CHAR16 02 E4B7    CHARGE 02 E750    CHPOST 02 E465    CHTCHI 02 E4C3
    CHTDON 02 E4C6    CLIGNE 02 E7A6    CLIGNO 02 E7A8    CLRDIS 02 E29D
    CODFIN 02 E68D    COLSUI 02 E050    COMDEC 02 07EE    COMPAB 02 E43A
    COMPAR 02 E4BB    COMPR2 02 E52E    COMPR3 02 E53A    COMPR4 02 E54C
    COMPR5 02 E575    CONHEX 02 E11A    CONROT 02 E5E3     CONTI 02 E208
   D6809UP 02 E807    DEBCHA 02 E6E8    DECENC 02 E58D    DECHEX 02 E50C
    DECKEY 02 E05B     DECRE 02 E1AF    DECTJS 02 E593    DEPLAC 02 E4FA
    DERADD 02 E645    DETECS 02 E609    DIGTBL 02 E010    DISBUF 02 07FA
    DISCNT 00 A005    DISFIN 02 E64F    DISPRE 02 E07B    DISREG 00 A004
      DLY1 02 E0AD      DLY2 02 E06D    DLY30U 02 E5BD    DLY5MS 02 E6F2
     DONEA 02 E12D    DPLOOP 02 E816    ENCDON 02 E148    ENCORE 02 E2AE
    EREURE 02 E440    ERREUR 02 E43E     ERROR 02 E7B5      EXCN 02 E283
     EXDEC 02 E180     EXINC 02 E182    EXLBCL 02 E758    EXLBIL 02 E75A
    EXLOAD 02 E5F1    EXMEMO 02 E12F    EXOFST 02 E71D    EXPOCT 02 E1B5
    EXPUNC 02 E659    EXREGI 02 E367    FADDRX 02 E0B7    FINCAL 02 E589
    FINCHA 02 E6DD    FINCLA 02 E026    FINENR 02 E6BC    FONCTI 02 E126
    FONREG 02 E31F    GETKEY 02 E020    HEXCON 02 E0EF    HEXIN7 02 E0D4
    INCREM 02 E16B     INTER 02 E30C    INTER2 02 E2B4    INTER3 02 E5AF
    INTR05 02 E44C    INTR08 02 E47F    INTR16 02 E4A0    INTVAL 02 E4D7
    KBTEST 02 E81B    KEYHEX 02 E0EC    KEYTBL 02 E000     L7SEG 02 E104
    LIGSUI 02 E03B    LSBDSA 02 E58B    M10000 02 E563      MABS 00 0760
     MOINS 02 E202    MSBDON 02 E4CC    MSBLSB 02 E59A    MUL100 02 E5A3
    MULFIN 02 E546    MULPAR 02 E525    MUP100 02 E5A9    NDVALH 02 E10F
    NEGPOS 02 07E9    NONFIN 02 E122     NOREB 02 E06C    NOUDLY 02 E5B5
    OFFSET 02 E1CA    OFPOCT 02 E730      PILE 00 07C0    PILMON 00 07A0
    PLUCOU 02 E77B      PLUS 02 E1FB    PLUSMS 02 07E3    POCPOS 02 E461
   POSITIF 02 E46F    PRELOW 02 07E7    PRESEH 02 07E5    PRESEL 02 07E6
    PRESER 02 07E1    PREXIN 02 E18A    PROMPT 02 E2A9     R7SEG 02 E108
    R8BREG 02 E3B5       RCR 02 E49C    RE8BIT 02 E356    REBIT8 02 E35B
    RECBIT 02 E5B3    RECMCE 02 E557     RECOM 02 E098    RECYCL 02 E683
    REG16B 02 E3C0     REGPC 02 E360    REGPRE 02 E334    REGSUI 02 E313
    REGTBL 02 E2B6    REMEMO 02 E138    RESTAR 02 E22E    RETOU1 02 E787
    RETOU2 02 E78B    RETOUR 02 E4F9     RFIRQ 00 0775      RIRQ 00 0770
      RNMI 02 E802    ROUNMI 02 E287    RPOINT 02 E23C      RSWI 02 E2BF
     RSWI2 02 E291     RSWI3 00 077A    SADDR1 02 07F2    SADDR2 02 07F4
    SASWI2 02 07DF      SAUT 02 E700    SAUVER 02 07E2     SAVB1 02 07F6
     SAVB2 02 07F7    SAVCNT 02 07F0    SAVNMI 02 07DD    SAVPIL 02 07F8
    SAVPOC 02 07E4    SAVREG 02 07F1    SAVRES 02 07EF    SCARRY 02 E5B7
    SCNCNT 00 A007    SCNREG 00 A006    SCRUDE 02 E32E    SCRUTA 02 E09F
    SCRUTC 02 E0F6    SECPAR 02 E565    SETMBS 02 E220       SM1 02 E227
       SM2 02 E229    STADON 02 E49A    STOCKA 02 E7E0    STODN1 02 E494
    STODON 02 E492       SUI 02 E4EB    SUICHA 02 E637    SUITEB 02 E768
    SUITEP 02 E4E0    SUITER 02 E2E6     SUIVT 02 E7D6     TEMPO 02 E7BB
    TESTER 02 E56F    TESTST 02 E432    TJTRAN 02 E6A8    TOUREG 02 E2D3
    TRANSM 02 E6F0    TSTDEC 02 E328    TSTPOC 02 E45C    TSTSUI 02 E445
    VALHEX 02 E116
213 SYMBOLS

0 error(s), 0 warning(s)
